;
; CS161 HW3: Sokoban
;
; *********************
;    READ THIS FIRST
; *********************
;
; All functions that you need to modify are marked with 'EXERCISE' in their
; header comments. This file also contains many helper functions. You may call
; any of them in your functions.
;
; Do not modify a-star.lsp.
;
; *Warning*: The provided A* code only supports the maximum cost of 4999 for any
; node. That is f(n)=g(n)+h(n) < 5000. So, be careful when you write your
; heuristic functions.  Do not make them return anything too large.
;
; For Allegro Common Lisp users: The free version of Allegro puts a limit on
; memory. So, it may crash on some hard sokoban problems and there is no easy
; fix (unless you buy Allegro). Of course, other versions of Lisp may also crash
; if the problem is too hard, but the amount of memory available will be
; relatively more relaxed. Improving the quality of the heuristic will mitigate
; this problem, as it will allow A* to solve hard problems with fewer node
; expansions. In either case, this limitation should not significantly affect
; your grade.
; 
; Remember that most functions are not graded on efficiency (only correctness).
; Efficiency can only influence your heuristic performance in the competition
; (which will affect your score).
;  


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; General utility functions
; They are not necessary for this homework.
; Use/modify them for your own convenience.
;

;
; For reloading modified code.
; I found this easier than typing (load "filename") every time.
;
(defun reload()
  (load "hw3.lsp")
  )

;
; For loading a-star.lsp.
;
(defun load-a-star()
  (load "a-star.lsp"))

;
; Reloads hw3.lsp and a-star.lsp
;
(defun reload-all()
  (reload)
  (load-a-star)
  )

;
; A shortcut function.
; goal-test and next-states stay the same throughout the assignment.
; So, you can just call (sokoban <init-state> #'<heuristic-name>).
; 
(defun sokoban (s h)
  (a* s #'goal-test #'next-states h)
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; end general utility functions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We now begin actual Sokoban code
;

; Define some global variables
(setq blank 0)
(setq wall 1)
(setq box 2)
(setq keeper 3)
(setq star 4)
(setq boxstar 5)
(setq keeperstar 6)

; Some helper functions for checking the content of a square
(defun isBlank (v)
  (equal v blank)
  )

(defun isWall (v)
  (equal v wall)
  )

(defun isBox (v)
  (equal v box)
  )

(defun isKeeper (v)
  (equal v keeper)
  )

(defun isStar (v)
  (equal v star)
  )

(defun isBoxStar (v)
  (equal v boxstar)
  )

(defun isKeeperStar (v)
  (equal v keeperstar)
  )

;
; Helper function of getKeeperPosition
;
(defun getKeeperColumn (r col)
  (cond ((null r) nil)
	(t (if (or (isKeeper (car r)) (isKeeperStar (car r)))
	       col
	     (getKeeperColumn (cdr r) (+ col 1))
	     );end if
	   );end t
	);end cond
  )

;
; getKeeperPosition (s firstRow)
; Returns a list indicating the position of the keeper (c r).
; 
; Assumes that the keeper is in row >= firstRow.
; The top row is the zeroth row.
; The first (right) column is the zeroth column.
;
(defun getKeeperPosition (s row)
  (cond ((null s) nil)
	(t (let ((x (getKeeperColumn (car s) 0)))
	     (if x
		 ;keeper is in this row
		 (list x row)
		 ;otherwise move on
		 (getKeeperPosition (cdr s) (+ row 1))
		 );end if
	       );end let
	 );end t
	);end cond
  );end defun

;
; cleanUpList (l)
; returns l with any NIL element removed.
; For example, if l is '(1 2 NIL 3 NIL), returns '(1 2 3).
;
(defun cleanUpList (L)
  (cond ((null L) nil)
	(t (let ((cur (car L))
		 (res (cleanUpList (cdr L)))
		 )
	     (if cur
		 (cons cur res)
		  res
		 )
	     );end let
	   );end t
	);end cond
  );end

; EXERCISE: Modify this function to return true (t)
; if and only if s is a goal state of a Sokoban game.
; (no box is on a non-goal square)
;
; Currently, it always returns NIL. If A* is called with
; this function as the goal testing function, A* will never
; terminate until the whole search space is exhausted.
;
; If there is a box on the map, return nil. Else, return true.
(defun goal-test (s)
  (cond
    ((null s) t) ; an empty map doesn't have boxes on non-goal squares
    ((has box (first s)) nil)
    (t (goal-test (rest s)))
  )
);end defun

; HAS returns TRUE if ITEM is in ITEMS, else NIL.
(defun has (item items)
  (cond
    ((null items) nil)
    ((equal item (first items)) t)
    (t (has item (rest items)))
  )
)


; EXERCISE: Modify this function to return the list of
; sucessor states of s.
;
; This is the top-level next-states (successor) function.
; Some skeleton code is provided below.
; You may delete them totally, depending on your approach.
;
; If you want to use it, you will need to set 'result' to be
; the set of states after moving the keeper in each of the 4 directions.
; A pseudo-code for this is:
;
; ...
; (result
;   (list
;     (try-move s UP)
;     (try-move s DOWN)
;     (try-move s LEFT)
;     (try-move s RIGHT)
;   )
; )
; ...
; 
; You will need to define the function try-move and decide how to represent UP,
; DOWN, LEFT, RIGHT. Any NIL result returned from try-move can be removed by
; cleanUpList.
;
(defun next-states (s)
  (cleanUpList 
    (list 
      (try-move s 'up) 
      (try-move s 'down) 
      (try-move s 'left) 
      (try-move s 'right)
    )
  )
);

; TRY-MOVE attempts to move the keeper in the DIRECTION given, either 'up,
; 'down, 'left, or 'right. STATE is the representation of the game. This
; function returns NIL if the move is not possible; otherwise, it returns
; the new state of the game
(defun try-move (state direction)
  (let* (
      (position (getKeeperPosition state 0))
	    (x (car position))
	    (y (cadr position))
      (above (get-square state (decrement y) x))
      (below (get-square state (increment y) x))
      (onleft (get-square state y (decrement x)))
      (onright (get-square state y (increment x)))
    )
    (cond
      (
        (equal direction 'up)
        (cond 
          ((isEmpty above) (moveKeeper state (decrement y) x))
          (
            (isBoxish above)
            (moveKeeper (moveBox state (list x (decrement y)) (decrement (decrement y)) x) (decrement y) x)
          )
          (t nil)
        )
      )
      (
        (equal direction 'down)
        (cond 
          ((isEmpty below) (moveKeeper state (increment y) x))
          (
            (isBoxish below)
            (moveKeeper (moveBox state (list x (increment y)) (increment (increment y)) x) (increment y) x)
          )
          (t nil)
        )
      )
      (
        (equal direction 'left)
        (cond 
          ((isEmpty onLeft) (moveKeeper state y (decrement x)))
          (
            (isBoxish onLeft)
            (moveKeeper (moveBox state (list (decrement x) y) y (decrement (decrement x))) y (decrement x))
          )
          (t nil)
        )
      )
      (
       (equal direction 'right)
       (cond 
         ((isEmpty onRight) (moveKeeper state y (increment x)))
         (
           (isBoxish onRight)
           (moveKeeper (moveBox state (list (increment x) y) y (increment (increment x))) y (increment x))
         )
         (t nil)
       )
      )
      (t nil)
    )
  )
)

; MOVEBOX takes as its arguments the STATE of the game, the current POSITION of
; box as a list containing the box's x and y coordinates, and the ROW and
; COLUMN to which the box is desired to be moved. If the move is possible,
; the resulting state is returned. Otherwise, NIL.
(defun moveBox (state position row column)
  (cond 
    (
      (isEmpty (get-square state row column))
      (let* (
          (x (first position))
          (y (second position))
          (boxOld (cond ((isBox (get-square state y x)) blank) (t star)))
          (boxNew 
            (cond ((isStar (get-square state row column)) boxstar) (t box))
          )
        )
        (set-square (set-square state row column boxNew) y x boxOld)
      )
    )
    (t nil)
  )
)

; MOVEKEEPER takes as its arguments the STATE of the game, and the ROW and
; COLUMN to which the keeper is desired to move. If the input STATE is nil,
; nil is returned. Otherwise, the new state with a moved keeper is returned.
; MOVEKEEPER assumes that the move is possible and accordingly makes no effort
; to validate its inputs.
(defun moveKeeper (state row column)
  (cond 
    ((null state) nil)
    (t
      (let* (
          (position (getKeeperPosition state 0))
	        (x (car position))
	        (y (cadr position))
          (keeperOld (cond ((isKeeper (get-square state y x)) blank) (t star)))
          (keeperNew 
            (cond
              ((isStar (get-square state row column)) keeperstar)
              (t keeper)
            )
          )
        )
        (set-square (set-square state row column keeperNew) y x keeperOld)
      )
    )
  )
)

; ISEMPTY returns true if the input SQUARE is either a blank or a star, else
; nil.
(defun isEmpty (square) (or (isBlank square) (isStar square)))

; ISBOXISH returns true if the input SQUARE is either a box or a boxstar, else
; nil.
(defun isBoxish (square) (or (isBox square) (isBoxStar square)))

; INCREMENT returns NUMBER incremented by 1.
(defun increment (number) (+ number 1))

; DECREMENT returns NUMBER decremented by 1.
(defun decrement (number) (- number 1))

; GET-SQUARE returns the value located at row R and column C in state S.
(defun get-square (s r c)
  (cond
    ((outOfBounds s r c) nil)
    ((> r 0) (get-square (rest s) (- r 1) c))
    ((> c 0) (get-square (list (rest (first s))) r (- c 1)))
    (t (first (first s)))
  )
)

; OUTOFBOUNDS returns true if and only if the coordinate specified by row R and
; column C is out of the bounds of state S.
(defun outOfBounds (s r c)
  (or (< r 0) (< c 0) (> r (- (length s) 1)) (> c (- (length (first s)) 1)))
)

; SET-SQUARE sets the value of the square located at row R and column C in state
; S to the value V.
(defun set-square (s r c v)
  (cond
    ((outOfBounds s r c) nil)
    ((> r 0) (cons (first s) (set-square (rest s) (- r 1) c v)))
    (t (append (list (set-value (first s) c v)) (rest s)))
  )
)

; SET-VALUE sets the item located at INDEX in ITEMS to VALUE;
(defun set-value (items index value)
  (cond
    (
      (> index 0)
      (cons (first items) (set-value (rest items) (- index 1) value))
    )
    (t (cons value (rest items)))
  )
)

; EXERCISE: Modify this function to compute the trivial
; admissible heuristic.
;
(defun h0 (s)
  0
)

; EXERCISE: Modify this function to compute the
; number of misplaced boxes in s.
;
; PROOF OF ADMISSABILITY. The heuristic is at maximum when when none of the
; N boxes are sitting on goal squares. In this case, it will take at least
; N moves to solve the game, and thus this heuristic never overestimates the
; cost of reaching the goal state.
(defun h1 (s)
  (cond
    ((null s) 0)
    (t (+ (countBoxes (first s) 0) (h1 (rest s))))
  )
)

(defun countBoxes (row carry)
  (cond
    ((null row) carry)
    ((isBox (first row)) (countBoxes (rest row) (+ 1 carry)))
    (t (countBoxes (rest row) carry))
  )
)

; EXERCISE: Change the name of this function to h<UID> where
; <UID> is your actual student ID number. Then, modify this
; function to compute an admissible heuristic value of s.
;
; This function will be entered in the competition.
; Objective: make A* solve problems as fast as possible.
; The Lisp 'time' function can be used to measure the
; running time of a function call.
;
; For each row, compute the absolute value of the difference between how many
; boxes there are in that row and how many goals there are in that row. Then
; return the sum of all of these row differences.
;
; PROOF OF ADMISSABILITY. The heuristic is at maximum when when none of the
; N boxes are in the same row as the goals. In this case, it will take at least
; N moves to solve the game, and thus this heuristic never overestimates the
; cost of reaching the goal state.
(defun h204156534 (s)
  (cond
    ((null s) 0)
    (t (+ (delta (first s) 0 0) (h204156534 (rest s))))
  )
)

; DELTA returns the absolute value of the difference between the number of 
; boxes and goals in ROW. BOXCARRY and GOALCARRY are both expected to have value
; 0 when DELTA is called by a function other than itself.
(defun delta (row boxCarry goalCarry)
  (cond
    ((null row) (absolute (- boxCarry goalCarry)))
    ((isBox (first row)) (delta (rest row) (+ 1 boxCarry) goalCarry))
    ((isStar (first row)) (delta (rest row) boxCarry (+ 1 goalCarry)))
    (t (delta (rest row) boxCarry goalCarry))
  )
)

; ABSOLUTE returns the absolute value of VALUE.
(defun absolute (number)
  (cond
    ((< number 0) (- number))
    (t number)
  )
)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

#|
 | Some predefined problems.  Each problem can be visualized by calling
 | (printstate <problem>).  Problems are ordered roughly by their difficulties.
 | For most problems, we also privide 2 additional number per problem: 1) # of
 | nodes expanded by A* using our next-states and h0 heuristic.  2) the depth of
 | the optimal solution.  These numbers are located at the comments of the
 | problems. For example, the first problem below was solved by 80 nodes
 | expansion of A* and its optimal solution depth is 7.
 |
 | Your implementation may not result in the same number of nodes expanded, but
 | it should probably give something in the same ballpark. As for the solution
 | depth, any admissible heuristic must make A* return an optimal solution. So,
 | the depths of the optimal solutions provided could be used for checking
 | whether your heuristic is admissible.
 |
 | Warning: some problems toward the end are quite hard and could be impossible
 | to solve without a good heuristic!
 |
 |#

;(80,7)
(setq p1 '((1 1 1 1 1 1)
	   (1 0 3 0 0 1)
	   (1 0 2 0 0 1)
	   (1 1 0 1 1 1)
	   (1 0 0 0 0 1)
	   (1 0 0 0 4 1)
	   (1 1 1 1 1 1)))

;(110,10)
(setq p2 '((1 1 1 1 1 1 1)
	   (1 0 0 0 0 0 1) 
	   (1 0 0 0 0 0 1) 
	   (1 0 0 2 1 4 1) 
	   (1 3 0 0 1 0 1)
	   (1 1 1 1 1 1 1)))

;(211,12)
(setq p3 '((1 1 1 1 1 1 1 1 1)
	   (1 0 0 0 1 0 0 0 1)
	   (1 0 0 0 2 0 3 4 1)
	   (1 0 0 0 1 0 0 0 1)
	   (1 0 0 0 1 0 0 0 1)
	   (1 1 1 1 1 1 1 1 1)))

;(300,13)
(setq p4 '((1 1 1 1 1 1 1)
	   (0 0 0 0 0 1 4)
	   (0 0 0 0 0 0 0)
	   (0 0 1 1 1 0 0)
	   (0 0 1 0 0 0 0)
	   (0 2 1 0 0 0 0)
	   (0 3 1 0 0 0 0)))

;(551,10)
(setq p5 '((1 1 1 1 1 1)
	   (1 1 0 0 1 1)
	   (1 0 0 0 0 1)
	   (1 4 2 2 4 1)
	   (1 0 0 0 0 1)
	   (1 1 3 1 1 1)
	   (1 1 1 1 1 1)))

;(722,12)
(setq p6 '((1 1 1 1 1 1 1 1)
	   (1 0 0 0 0 0 4 1)
	   (1 0 0 0 2 2 3 1)
	   (1 0 0 1 0 0 4 1)
	   (1 1 1 1 1 1 1 1)))

;(1738,50)
(setq p7 '((1 1 1 1 1 1 1 1 1 1)
	   (0 0 1 1 1 1 0 0 0 3)
	   (0 0 0 0 0 1 0 0 0 0)
	   (0 0 0 0 0 1 0 0 1 0)
	   (0 0 1 0 0 1 0 0 1 0)
	   (0 2 1 0 0 0 0 0 1 0)
	   (0 0 1 0 0 0 0 0 1 4)))

;(1763,22)
(setq p8 '((1 1 1 1 1 1)
	   (1 4 0 0 4 1)
	   (1 0 2 2 0 1)
	   (1 2 0 1 0 1)
	   (1 3 0 0 4 1)
	   (1 1 1 1 1 1)))

;(1806,41)
(setq p9 '((1 1 1 1 1 1 1 1 1) 
	   (1 1 1 0 0 1 1 1 1) 
	   (1 0 0 0 0 0 2 0 1) 
	   (1 0 1 0 0 1 2 0 1) 
	   (1 0 4 0 4 1 3 0 1) 
	   (1 1 1 1 1 1 1 1 1)))

;(10082,51)
(setq p10 '((1 1 1 1 1 0 0)
	    (1 0 0 0 1 1 0)
	    (1 3 2 0 0 1 1)
	    (1 1 0 2 0 0 1)
	    (0 1 1 0 2 0 1)
	    (0 0 1 1 0 0 1)
	    (0 0 0 1 1 4 1)
	    (0 0 0 0 1 4 1)
	    (0 0 0 0 1 4 1)
	    (0 0 0 0 1 1 1)))

;(16517,48)
(setq p11 '((1 1 1 1 1 1 1)
	    (1 4 0 0 0 4 1)
	    (1 0 2 2 1 0 1)
	    (1 0 2 0 1 3 1)
	    (1 1 2 0 1 0 1)
	    (1 4 0 0 4 0 1)
	    (1 1 1 1 1 1 1)))

;(22035,38)
(setq p12 '((0 0 0 0 1 1 1 1 1 0 0 0)
	    (1 1 1 1 1 0 0 0 1 1 1 1)
	    (1 0 0 0 2 0 0 0 0 0 0 1)
	    (1 3 0 0 0 0 0 0 0 0 0 1)
	    (1 0 0 0 2 1 1 1 0 0 0 1)
	    (1 0 0 0 0 1 0 1 4 0 4 1)
	    (1 1 1 1 1 1 0 1 1 1 1 1)))

;(26905,28)
(setq p13 '((1 1 1 1 1 1 1 1 1 1)
	    (1 4 0 0 0 0 0 2 0 1)
	    (1 0 2 0 0 0 0 0 4 1)
	    (1 0 3 0 0 0 0 0 2 1)
	    (1 0 0 0 0 0 0 0 0 1)
	    (1 0 0 0 0 0 0 0 4 1)
	    (1 1 1 1 1 1 1 1 1 1)))

;(41715,53)
(setq p14 '((0 0 1 0 0 0 0)
	    (0 2 1 4 0 0 0)
	    (0 2 0 4 0 0 0)
	    (3 2 1 1 1 0 0)
	    (0 0 1 4 0 0 0)))

;(48695,44)
(setq p15 '((1 1 1 1 1 1 1)
	    (1 0 0 0 0 0 1)
	    (1 0 0 2 2 0 1)
	    (1 0 2 0 2 3 1)
	    (1 4 4 1 1 1 1)
	    (1 4 4 1 0 0 0)
	    (1 1 1 1 0 0 0)
	    ))

;(91344,111)
(setq p16 '((1 1 1 1 1 0 0 0)
	    (1 0 0 0 1 0 0 0)
	    (1 2 1 0 1 1 1 1)
	    (1 4 0 0 0 0 0 1)
	    (1 0 0 5 0 5 0 1)
	    (1 0 5 0 1 0 1 1)
	    (1 1 1 0 3 0 1 0)
	    (0 0 1 1 1 1 1 0)))

;(3301278,76)
(setq p17 '((1 1 1 1 1 1 1 1 1 1)
	    (1 3 0 0 1 0 0 0 4 1)
	    (1 0 2 0 2 0 0 4 4 1)
	    (1 0 2 2 2 1 1 4 4 1)
	    (1 0 0 0 0 1 1 4 4 1)
	    (1 1 1 1 1 1 0 0 0 0)))

;(??,25)
(setq p18 '((0 0 0 0 1 0 0 0 0 0 0 1 0 0 0 0)
	    (0 0 0 0 1 0 0 0 0 0 0 1 0 0 0 0)
	    (1 1 1 1 1 0 0 0 0 0 0 1 1 1 1 1)
	    (0 0 0 0 0 1 0 0 0 0 1 0 0 0 0 0)
	    (0 0 0 0 0 0 1 0 0 1 0 0 0 0 0 0)
	    (0 0 0 0 0 0 0 0 3 0 0 0 0 0 0 0)
	    (0 0 0 0 0 0 1 0 0 1 0 0 0 0 0 0)
	    (0 0 0 0 0 1 0 0 0 0 1 0 0 0 0 0)
	    (1 1 1 1 1 0 0 0 0 0 0 1 1 1 1 1)
	    (0 0 0 0 1 0 0 0 0 0 0 1 0 0 0 0)
	    (0 0 0 0 1 0 0 0 0 0 0 1 0 0 0 0)
	    (0 0 0 0 1 0 0 0 0 0 4 1 0 0 0 0)
	    (0 0 0 0 1 0 2 0 0 0 0 1 0 0 0 0)
	    (0 0 0 0 1 0 2 0 0 0 4 1 0 0 0 0)
	    ))
;(??,21)
(setq p19 '((0 0 0 1 0 0 0 0 1 0 0 0)
	    (0 0 0 1 0 0 0 0 1 0 0 0)
	    (0 0 0 1 0 0 0 0 1 0 0 0)
	    (1 1 1 1 0 0 0 0 1 1 1 1)
	    (0 0 0 0 1 0 0 1 0 0 0 0)
	    (0 0 0 0 0 0 3 0 0 0 2 0)
	    (0 0 0 0 1 0 0 1 0 0 0 4)
	    (1 1 1 1 0 0 0 0 1 1 1 1)
	    (0 0 0 1 0 0 0 0 1 0 0 0)
	    (0 0 0 1 0 0 0 0 1 0 0 0)
	    (0 0 0 1 0 2 0 4 1 0 0 0)))

;(??,??)
(setq p20 '((0 0 0 1 1 1 1 0 0)
	    (1 1 1 1 0 0 1 1 0)
	    (1 0 0 0 2 0 0 1 0)
	    (1 0 0 5 5 5 0 1 0)
	    (1 0 0 4 0 4 0 1 1)
	    (1 1 0 5 0 5 0 0 1)
	    (0 1 1 5 5 5 0 0 1)
	    (0 0 1 0 2 0 1 1 1)
	    (0 0 1 0 3 0 1 0 0)
	    (0 0 1 1 1 1 1 0 0)))

;(??,??)
(setq p21 '((0 0 1 1 1 1 1 1 1 0)
	    (1 1 1 0 0 1 1 1 1 0)
	    (1 0 0 2 0 0 0 1 1 0)
	    (1 3 2 0 2 0 0 0 1 0)
	    (1 1 0 2 0 2 0 0 1 0)
	    (0 1 1 0 2 0 2 0 1 0)
	    (0 0 1 1 0 2 0 0 1 0)
	    (0 0 0 1 1 1 1 0 1 0)
	    (0 0 0 0 1 4 1 0 0 1)
	    (0 0 0 0 1 4 4 4 0 1)
	    (0 0 0 0 1 0 1 4 0 1)
	    (0 0 0 0 1 4 4 4 0 1)
	    (0 0 0 0 1 1 1 1 1 1)))

;(??,??)
(setq p22 '((0 0 0 0 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0)
	    (0 0 0 0 1 0 0 0 1 0 0 0 0 0 0 0 0 0 0)
	    (0 0 0 0 1 2 0 0 1 0 0 0 0 0 0 0 0 0 0)
	    (0 0 1 1 1 0 0 2 1 1 0 0 0 0 0 0 0 0 0)
	    (0 0 1 0 0 2 0 2 0 1 0 0 0 0 0 0 0 0 0)
	    (1 1 1 0 1 0 1 1 0 1 0 0 0 1 1 1 1 1 1)
	    (1 0 0 0 1 0 1 1 0 1 1 1 1 1 0 0 4 4 1)
	    (1 0 2 0 0 2 0 0 0 0 0 0 0 0 0 0 4 4 1)
	    (1 1 1 1 1 0 1 1 1 0 1 3 1 1 0 0 4 4 1)
	    (0 0 0 0 1 0 0 0 0 0 1 1 1 1 1 1 1 1 1)
	    (0 0 0 0 1 1 1 1 1 1 1 0 0 0 0 0 0 0 0)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

#|
 | Utility functions for printing states and moves.
 | You do not need to understand any of the functions below this point.
 |#

;
; Helper function of prettyMoves
; from s1 --> s2
;
(defun detectDiff (s1 s2)
  (let* ((k1 (getKeeperPosition s1 0))
	 (k2 (getKeeperPosition s2 0))
	 (deltaX (- (car k2) (car k1)))
	 (deltaY (- (cadr k2) (cadr k1)))
	 )
    (cond ((= deltaX 0) (if (> deltaY 0) 'DOWN 'UP))
	  (t (if (> deltaX 0) 'RIGHT 'LEFT))
	  );end cond
    );end let
  );end defun

;
; Translates a list of states into a list of moves.
; Usage: (prettyMoves (a* <problem> #'goal-test #'next-states #'heuristic))
;
(defun prettyMoves (m)
  (cond ((null m) nil)
	((= 1 (length m)) (list 'END))
	(t (cons (detectDiff (car m) (cadr m)) (prettyMoves (cdr m))))
	);end cond
  );

;
; Print the content of the square to stdout.
;
(defun printSquare (s)
  (cond ((= s blank) (format t " "))
	((= s wall) (format t "#"))
	((= s box) (format t "$"))
	((= s keeper) (format t "@"))
	((= s star) (format t "."))
	((= s boxstar) (format t "*"))
	((= s keeperstar) (format t "+"))
	(t (format t "|"))
	);end cond
  )

;
; Print a row
;
(defun printRow (r)
  (dolist (cur r)
    (printSquare cur)
    )
  );

;
; Print a state
;
(defun printState (s)
  (progn
    (dolist (cur s)
      (printRow cur)
      (format t "~%")
      )
    );end progn
  )

;
; Print a list of states with delay.
;
(defun printStates (sl delay)
  (dolist (cur sl)
    (printState cur)
    (sleep delay)
    );end dolist
  );end defun
